plugins {
    id 'java'
    id 'maven-publish'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'me.gabytm.util'
version '0.0.3-BETA'
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.okhttp3:okhttp:4.8.1'
    implementation 'org.jetbrains:annotations:20.1.0'
}

shadowJar {
    archiveName("${project.name} [${project.version}].jar")
    minimize()
}

javadoc {
    destinationDir = file("${buildDir}/docs/${project.name}/")

    options.encoding("UTF-8")

    if (options instanceof StandardJavadocDocletOptions) {
        def opt = options as StandardJavadocDocletOptions

        opt.links(
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://javadoc.io/doc/com.google.code.gson/gson/2.8.5/",
                "https://javadoc.io/doc/com.squareup.okhttp3/okhttp/3.14.9/",
                "https://javadoc.io/doc/org.jetbrains/annotations/20.1.0/"
        )

        if (JavaVersion.current().isJava9Compatible()) {
            opt.addBooleanOption("html5", true)
            opt.addStringOption("-release", "8")
        }

        if (JavaVersion.current().isJava11Compatible()) {
            opt.addBooleanOption("-no-module-directories", true)
        }
    }
}
